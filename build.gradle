apply plugin: 'eu.appsatori.fatjar'
apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'pmd'

version = "0.0.3"

def defaultEncoding = 'UTF-8'
[compileJava, compileTestJava]*.options*.encoding = defaultEncoding

repositories {
  mavenCentral()
}

buildscript {
  dependencies {
    classpath 'eu.appsatori:gradle-fatjar-plugin:0.3' // adds fatJar task
  }
  repositories {
    jcenter()
  }
}

dependencies {
  compile 'javax.mail:javax.mail-api:1.5.3'
  compile 'com.sun.mail:javax.mail:1.5.3'
  compile 'com.sendgrid:smtpapi-java:1.2.0'
}

// adds 'with-dependencies' to the fatJar name
fatJar {
  classifier 'jar'
  baseName "sendgrid-java-example"
  manifest {
    attributes("Implementation-Title": "SendgridJavaExample", "Implementation-Version": version)
  }
}

// copy fatJar to base project directory so they will be in git (and on github for download)
build << {
  copy {
    println "Copying ${fatJar.archiveName} to $projectDir/repo/com/github/sendgrid-java-example/$version"
    from("$buildDir/libs/${fatJar.archiveName}")
    into("$projectDir/repo/com/github/sendgrid-java-example/$version")
  }
}

artifacts {
  archives fatJar
}


tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
 }

tasks.withType(Pmd) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
 }
